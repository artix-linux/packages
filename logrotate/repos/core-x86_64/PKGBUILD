# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=logrotate
pkgver=3.14.0
pkgrel=1.1
pkgdesc="Rotates system logs automatically"
arch=('x86_64')
url="https://github.com/logrotate/logrotate"
license=('GPL')
groups=('base')
depends=('popt' 'gzip' 'acl' 'cron')
backup=('etc/logrotate.conf')
source=("https://github.com/logrotate/logrotate/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        'logrotate.conf')
md5sums=('1c0f6e6e490c4bcac0a1e77ad1310683'
         'SKIP'
         '94dae4d3eded2fab9ae879533d3680db')
validpgpkeys=('992A96E075056E79CD8214F9873DB37572A37B36')

build() {
	cd "$srcdir/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man \
		--with-compress-command=/usr/bin/gzip \
		--with-uncompress-command=/usr/bin/gunzip \
		--with-default-mail-command=/usr/bin/mail \
		--with-acl
	make
}

check() {
	cd "$srcdir/${pkgname}-${pkgver}"

	make test
}

package() {
	cd "$srcdir/${pkgname}-${pkgver}"

	make DESTDIR="$pkgdir" install

	install -dm755 "$pkgdir/etc/logrotate.d"
	install -Dm644 "$srcdir/logrotate.conf" "$pkgdir/etc/logrotate.conf"

	install -Dm755 examples/logrotate.cron "${pkgdir}"/etc/cron.daily/logrotate
}
